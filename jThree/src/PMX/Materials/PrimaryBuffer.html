<?xml version="1.0" encoding="UTF-8"?>
<material name="jthree.gbuffer.pmx.primary" group="jthree.materials.gbuffer.primary" order="300">
<uniform-register>
  <register name="jthree.basic.matrix"/>
</uniform-register>
<passes>
  <pass>
    <glsl>
      <![CDATA[
      attribute vec3 position;
      attribute vec3 normal;
      attribute vec2 uv;
      attribute vec4 boneWeights;
      attribute vec4 boneIndicies;
      varying vec3 vNormal;
      varying vec4 vPosition;

      //@vertonly{
        uniform mat4 _matVM;
        uniform mat4 _matPV;
        //@(register:0)
        uniform sampler2D boneMatriciesTexture;
        uniform float boneCount;

        mat4 matFromIndex(float index)
        {
        	float y =index/boneCount+1./boneCount/2.;
        	return mat4(
        	texture2D(boneMatriciesTexture,vec2(0.125,y)),
        	texture2D(boneMatriciesTexture,vec2(0.375,y)),
        	texture2D(boneMatriciesTexture,vec2(0.625,y)),
        	texture2D(boneMatriciesTexture,vec2(0.875,y)));
        }

        mat4 getBoneTransform()
        {
        	return boneWeights.x*matFromIndex(boneIndicies.x)
        	+boneWeights.y*matFromIndex(boneIndicies.y)
        	+boneWeights.z*matFromIndex(boneIndicies.z)
        	+boneWeights.w*matFromIndex(boneIndicies.w);
        }

        void main(void){
          mat4 boneTransform=getBoneTransform();
          vPosition=gl_Position = _matPV*boneTransform*vec4(position,1.0);
          vNormal=normalize((_matVM*vec4(normal,0)).xyz);
        }
      //}
      //@fragonly{
        uniform float brightness;

        vec2 compressNormal()
        {
        	float p = sqrt(vNormal.z * 8. + 8.);
        	return vNormal.xy/p + 0.5;
        }

        float calcDepth()
        {
        	return vPosition.z/vPosition.w;
        }

        void main(void)
        {
        	gl_FragColor = vec4(compressNormal(),calcDepth(),brightness);
        }
      //}
      ]]>
    </glsl>
  </pass>
</passes>
</material>
