<?xml version="1.0" encoding="UTF-8"?>
<material name="jthree.gbuffer.pmx.primary" group="jthree.materials.gbuffer.primary" order="300">
<uniform-register>
  <register name="jthree.basic.matrix"/>
</uniform-register>
<passes>
  <pass>
    <glsl>
      <![CDATA[
      attribute vec3 position;
      attribute vec3 normal;
      attribute vec2 uv;
      attribute vec4 boneWeights;
      attribute vec4 boneIndicies;
      varying vec3 vNormal;
      varying vec4 vPosition;
      varying vec2 vUV;
      varying vec2 vSphereUV;

      //@vertonly{
        uniform mat4 _matVM;
        uniform mat4 _matPV;
        //@(register:0)
        uniform sampler2D boneMatriciesTexture;
        uniform float boneCount;

        mat4 matFromIndex(float index)
        {
        	float y =index/boneCount+1./boneCount/2.;
        	return mat4(
        	texture2D(boneMatriciesTexture,vec2(0.125,y)),
        	texture2D(boneMatriciesTexture,vec2(0.375,y)),
        	texture2D(boneMatriciesTexture,vec2(0.625,y)),
        	texture2D(boneMatriciesTexture,vec2(0.875,y)));
        }

        mat4 getBoneTransform()
        {
        	return boneWeights.x*matFromIndex(boneIndicies.x)
        	+boneWeights.y*matFromIndex(boneIndicies.y)
        	+boneWeights.z*matFromIndex(boneIndicies.z)
        	+boneWeights.w*matFromIndex(boneIndicies.w);
        }

        void main(void){
          mat4 boneTransform=getBoneTransform();
          vPosition=gl_Position = _matPV*boneTransform*vec4(position,1.0);
          vNormal=normalize((_matVM*vec4(normal,0)).xyz);
          vSphereUV=vNormal.xy/2.+vec2(0.5,0.5);
          vUV=uv;
        }
      //}
      //@fragonly{
        uniform vec4 diffuse;
        //@(register:1)
        uniform sampler2D texture;
        //@(register:2)
        uniform sampler2D sphere;
        uniform int textureUsed;
        uniform int sphereMode;
        uniform vec4 addTextureCoefficient;
        uniform vec4 mulTextureCoefficient;
        uniform vec4 addSphereCoefficient;
        uniform vec4 mulSphereCoefficient;


        vec4 blendPMXTexture(sampler2D source,vec2 uv,vec4 addCoeff,vec4 mulCoeff)
        {
            vec4 result=texture2D(source,abs(fract(uv)));
            result.rgb=mix(mix(result.rgb,vec3(0,0,0),addCoeff.a),vec3(1,1,1),1.-mulCoeff.a);
            result.rgb=result.rgb*mulCoeff.rgb+addCoeff.rgb;
            return result;
        }

        void main(void){
            gl_FragColor.rgba=diffuse;
            if(textureUsed > 0){
                 gl_FragColor=blendPMXTexture(texture,vUV,addTextureCoefficient,mulTextureCoefficient);
            }
            if(sphereMode==1)
            {
              gl_FragColor.rgb*=blendPMXTexture(sphere,vSphereUV,addSphereCoefficient,mulSphereCoefficient).rgb;
            }else if(sphereMode==2)
            {
              gl_FragColor.rgb+=blendPMXTexture(sphere,vSphereUV,addSphereCoefficient,mulSphereCoefficient).rgb;
            }
        }
      //}
      ]]>
    </glsl>
  </pass>
</passes>
</material>
